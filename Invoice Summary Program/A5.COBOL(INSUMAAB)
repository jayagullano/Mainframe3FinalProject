       IDENTIFICATION DIVISION.
      *
       PROGRAM-ID. INSUMAAB.
      *
       ENVIRONMENT DIVISION.
      *
       DATA DIVISION.
      *
       WORKING-STORAGE SECTION.
      *
       01  SWITCHES.
      *
           05  INVOICE-EOF-SW          PIC X(01)    VALUE 'N'.
               88  INVOICE-EOF                      VALUE 'Y'.
           05  FIRST-RECORD-SW         PIC X(01)    VALUE 'Y'.
               88  FIRST-RECORD                     VALUE 'Y'.
      *
       01  WORK-FIELDS.
      *
           05  INVOICE-COUNT           PIC S9(05)    COMP-3  VALUE ZERO.
           05  INVOICE-TOTAL           PIC S9(07)V99 COMP-3  VALUE ZERO.
      *
       01  RESPONSE-CODE               PIC S9(08)    COMP.

       01 FILE-STATUS-INFO.
           02  INV-OPEN                PIC X VALUE 'N'.
           02  INV-OPEN-STATUS         PIC S9(8) COMP.
           02  INV-ENABLE-STATUS       PIC S9(8) COMP.
      *
       COPY SUMSAAB.
      *
       COPY INVOICE.
      *
       COPY ERRPARM.
      *
       PROCEDURE DIVISION.
      *
       0000-PREPARE-INVOICE-SUMMARY.
      *
           MOVE LOW-VALUE TO SUMMAABO.
           PERFORM 1000-START-INVOICE-BROWSE.
           PERFORM 2000-READ-NEXT-INVOICE
               UNTIL INVOICE-EOF.
           PERFORM 3000-END-INVOICE-BROWSE.
           PERFORM 4000-SEND-SUMMARY-MAP.

           EXEC CICS
               INQUIRE FILE('INVAAB')
                    OPENSTATUS(INV-OPEN-STATUS)
                    ENABLESTATUS(INV-ENABLE-STATUS)
           END-EXEC.

           IF INV-OPEN-STATUS = DFHVALUE(OPEN)
               EXEC CICS
                    SET FILE('INVAAB') CLOSED
               END-EXEC
               MOVE 'N' TO INV-OPEN
           END-IF.
      *
           EXEC CICS
               RETURN TRANSID('UAAB')
           END-EXEC.
      *
       1000-START-INVOICE-BROWSE.
      *
           MOVE 0 TO INV-INVOICE-NUMBER

           EXEC CICS
               INQUIRE FILE('INVAAB')
                    OPENSTATUS(INV-OPEN-STATUS)
                    ENABLESTATUS(INV-ENABLE-STATUS)
           END-EXEC.

           IF INV-OPEN-STATUS = DFHVALUE(CLOSED)
               EXEC CICS
                    SET FILE('INVAAB') OPEN
               END-EXEC
               MOVE 'Y' TO INV-OPEN
           END-IF.
      *
           EXEC CICS
               STARTBR FILE('INVAAB')
                       RIDFLD(INV-INVOICE-NUMBER)
                       RESP(RESPONSE-CODE)
           END-EXEC.
      *
           IF RESPONSE-CODE = DFHRESP(NOTFND)
               MOVE 'Y' TO INVOICE-EOF-SW
           ELSE
               IF RESPONSE-CODE NOT = DFHRESP(NORMAL)
                   PERFORM 9999-TERMINATE-PROGRAM
               END-IF
           END-IF.
      *
       2000-READ-NEXT-INVOICE.
      *
           EXEC CICS
               INQUIRE FILE('INVAAB')
                    OPENSTATUS(INV-OPEN-STATUS)
                    ENABLESTATUS(INV-ENABLE-STATUS)
           END-EXEC.

           IF INV-OPEN-STATUS = DFHVALUE(CLOSED)
               EXEC CICS
                    SET FILE('INVAAB') OPEN
               END-EXEC
               MOVE 'Y' TO INV-OPEN
           END-IF.

           EXEC CICS
               READNEXT FILE('INVAAB')
                        INTO(INVOICE-RECORD)
                        RIDFLD(INV-INVOICE-NUMBER)
                        RESP(RESPONSE-CODE)
           END-EXEC.
      *
           EVALUATE RESPONSE-CODE
               WHEN DFHRESP(NORMAL)
                   MOVE INV-INVOICE-NUMBER TO LASTO
                   ADD 1 TO INVOICE-COUNT
                   ADD INV-INVOICE-TOTAL TO INVOICE-TOTAL
                   IF FIRST-RECORD
                       MOVE INV-INVOICE-NUMBER TO FIRSTO
                       MOVE 'N' TO FIRST-RECORD-SW
                   END-IF
               WHEN DFHRESP(ENDFILE)
                   MOVE 'Y' TO INVOICE-EOF-SW
               WHEN OTHER
                   PERFORM 9999-TERMINATE-PROGRAM
           END-EVALUATE.
      *
       3000-END-INVOICE-BROWSE.
      *
           EXEC CICS
               ENDBR FILE('INVAAB')
                     RESP(RESPONSE-CODE)
           END-EXEC.
      *
           IF RESPONSE-CODE NOT = DFHRESP(NORMAL)
               PERFORM 9999-TERMINATE-PROGRAM
           END-IF.
      *
       4000-SEND-SUMMARY-MAP.
      *
           MOVE 'SAAB'        TO TRANIDO.
           MOVE INVOICE-COUNT TO COUNTO.
           MOVE INVOICE-TOTAL TO TOTALO.
      *
           EXEC CICS
               SEND MAP('SUMMAAB')
                    MAPSET('SUMSAAB')
                    FROM(SUMMAABO)
                    ERASE
           END-EXEC.
      *

       9999-TERMINATE-PROGRAM.
      *
           MOVE EIBRESP  TO ERR-RESP.
           MOVE EIBRESP2 TO ERR-RESP2.
           MOVE EIBTRNID TO ERR-TRNID.
           MOVE EIBRSRCE TO ERR-RSRCE.
      *
           EXEC CICS
               XCTL PROGRAM('SYSERR')
                    COMMAREA(ERROR-PARAMETERS)
           END-EXEC.

